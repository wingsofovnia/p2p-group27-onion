plugins {
    id "java"
    id "com.google.protobuf" version "0.8.1"
}

group 'de.tum.p2p'
version '0.1-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

ext {
    dependencyVersions = [
        commons: [
            apacheLang3: '3.5',
            ini4j: '0.5.+'
        ],
        net: [
            netty: '4.1.11.Final',
            protobuff: '3.3.1',
            protobuffCompiler: '3.3.0'
        ],
        gen: [
            lombok: '1.16.+'
        ],
        test: [
            junit: '4.12'
        ]
    ]
}

dependencies {
    compileOnly group: 'org.projectlombok', name: 'lombok', version: dependencyVersions.gen.lombok
    testCompileOnly group: 'org.projectlombok', name: 'lombok', version: dependencyVersions.gen.lombok

    compile group: 'org.apache.commons', name: 'commons-lang3', version: dependencyVersions.commons.apacheLang3
    compile group: 'org.ini4j', name: 'ini4j', version: dependencyVersions.commons.ini4j
    compile group: 'io.netty', name: 'netty-all', version: dependencyVersions.net.netty
    compile group: 'com.google.protobuf', name: 'protobuf-java', version: dependencyVersions.net.protobuff


    testCompile group: 'junit', name: 'junit', version: dependencyVersions.test.junit
}

protobuf {
    generatedFilesBaseDir = "$projectDir/src/generated"

    protoc {
        artifact = "com.google.protobuf:protoc:${dependencyVersions.net.protobuffCompiler}"
    }
}

sourceSets {
    main {
        java {
            srcDirs = ["src/main/java", "${protobuf.generatedFilesBaseDir}/main/java"]
        }
    }
}

clean {
    delete fileTree('.') { include '**/*.log' }
    delete protobuf.generatedFilesBaseDir
}
